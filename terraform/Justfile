# Show all the tasks
list:
  @just -lu

# Reformat all files
format:
  tofu fmt --recursive
  terragrunt hclfmt

# Check the format of all files
check-format:
  tofu fmt --recursive --check
  terragrunt hclfmt --terragrunt-check

# Run linters
lint:
  tflint --init
  find . -type d -not \( -path "**/.terraform*" -o -name "providers" \) | \
    xargs -I path tflint -c $(pwd)/.tflint.hcl --chdir path

# Ensure the project is valid
validate:
  terragrunt run-all validate

# Open a console
console:
  terragrunt console

# Plan the changes
plan:
  terragrunt plan

# Apply the changes
apply:
  terragrunt apply

# Checks to run in CI
ci: check-format lint validate

alias c := console
alias f := format
alias fmt := format
alias l := lint
alias v := validate
