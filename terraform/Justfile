# Show all the tasks
list:
  @just -lu

# Initialize the project
init:
  tofu init
  tflint --init

# Ensure the formatting is correct
format *FLAGS:
  tofu fmt -recursive {{FLAGS}}

# Run linters
lint:
  tflint --module --recursive

# Ensure the project is valid
validate:
  tofu validate

# Open a console
console:
  tofu console

# Plan the changes
plan:
  tofu plan

# Checks to run in CI
ci: (format "--check") lint validate

alias c := console
alias f := format
alias fmt := format
alias l := lint
alias v := validate
